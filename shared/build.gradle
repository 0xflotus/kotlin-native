/*
 * Copyright 2010-2017 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import org.jetbrains.kotlin.VersionGenerator

buildscript {
    ext.rootBuildDirectory = file('..')

    apply from: "$rootBuildDirectory/gradle/loadRootProperties.gradle"
    apply from: "$rootBuildDirectory/gradle/kotlinGradlePlugin.gradle"

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'kotlin'
apply plugin: 'com.jfrog.bintray'
apply plugin: "maven-publish"

group = 'org.jetbrains.kotlin'
version = konanVersion

repositories {
    mavenCentral()
    maven {
        url kotlinCompilerRepo
    }
}

task generateCompilerVersion(type: VersionGenerator){}

sourceSets {
    main.kotlin {
        srcDir 'src/main/kotlin'
        srcDir generateCompilerVersion.versionSourceDirectory
    }
}

compileKotlin {
    dependsOn('generateCompilerVersion')
}

clean {
    doFirst {
        if (generateCompilerVersion.versionSourceDirectory.exists())
             generateCompilerVersion.versionSourceDirectory.delete()
    }
}

jar {
    archiveName = "shared.jar"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$buildKotlinVersion"
}

publishing {
    publications {
        utils(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'kotlin-eap'
        name = 'kotlin-native-utils'
        userOrg = 'kotlin'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/JetBrains/kotlin-native'
        version {
            name = project.version
            desc = "Kotlin/Native utils, version $konanVersion)"
        }
        publish  = project.findProperty("bintrayPublish").toString().toBoolean()
        override = project.findProperty("bintrayOverride").toString().toBoolean()
    }
    publications = ['utils']
}
